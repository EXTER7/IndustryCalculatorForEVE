apply plugin: 'com.android.application'

buildscript {
    repositories {
        mavenCentral() // or jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta6'
    }
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.exter.eveindcalc"
        minSdkVersion 10
        targetSdkVersion 23

        versionName "3.2.7.0"
        versionCode 105
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

task extractAssetsFromEID(type: Copy) {
   from(zipTree('eid.zip'))
   into 'src/main/assets'
}

task cleanAssets(type: Delete) {
    def assetsDir = "src/main/assets" 
    file( assetsDir ).list().each{
        f ->
             delete "${assetsDir}/${f}"
    }
}


preBuild.dependsOn extractAssetsFromEID
clean.dependsOn cleanAssets
extractAssetsFromEID.dependsOn cleanAssets

dependencies {
    compile project(':TSL')
    compile project(':EVEIndustry')
    compile project(':EVEIndustryData')
    compile 'com.android.support:support-v4:23.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.android.gms:play-services:8.4.0'
}
